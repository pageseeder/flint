plugins {
  id "com.jfrog.bintray" version "1.7"
}

ext {
  globalVersion = file('version.txt').text
}
group       = 'org.pageseeder.flint'
description = "$title"
version     = "$globalVersion"

apply plugin: 'java'
apply plugin: 'maven-publish'
apply from: 'gradle/publishing.gradle'

allprojects {
  group   = 'org.pageseeder.flint'
  version = "$globalVersion"

  apply plugin: 'java'
  apply plugin: 'com.jfrog.bintray'
  
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    // allette ivy
    ivy   {
      url 'http://ivy.pageseeder.com/shared/'
      layout "pattern", {
        artifact  "[organisation]/[module]/[module]-[revision].[ext]"
      }
    }
    maven { url 'http://dl.bintray.com/pageseeder/maven' }
    jcenter()
  }
  
  jar {
    manifest {
      attributes 'Implementation-Vendor':  'Allette Systems',
                 'Implementation-Title':   "$title",
                 'Implementation-Version': "$globalVersion"
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.1'
  description 'Generates the Gradle wrapper scripts'
}

task publishAllToBintray {
  allprojects.each {
    dependsOn("${it.path}:bintrayUpload" )
  }
}

task publishAllToMavenLocal {
  allprojects.each {
    dependsOn("${it.path}:publishToMavenLocal" )
  }
}

dependencies {

  compile (
    'org.slf4j:slf4j-api:1.7.21',
    'org.pageseeder.xmlwriter:pso-xmlwriter:1.0.2'    
  )

  // runtime
  runtime (
    'net.sf.saxon:Saxon-HE:9.6.0-6'
  )

  testCompile (
    'junit:junit:4.12',
    'org.slf4j:slf4j-simple:1.7.21'
  )

}
